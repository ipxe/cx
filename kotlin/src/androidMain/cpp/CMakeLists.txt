cmake_minimum_required(VERSION 3.0)
include(ExternalProject)

# Configuration
set(OPENSSL_VERSION 1.1.1g)
set(OPENSSL_MD5 76766e98997660138cdaf13a187bd234)

# Construct local installation directories
set(INSTALL_DIR ${CMAKE_CURRENT_BINARY_DIR}/install)
set(INSTALL_PREFIX /usr)
set(INSTALL_INCLUDEDIR ${INSTALL_DIR}/${INSTALL_PREFIX}/include)
set(INSTALL_LIBDIR ${INSTALL_DIR}/${INSTALL_PREFIX}/lib)

# Path for external project tools
set(ANDROID_PATH ${ANDROID_TOOLCHAIN_ROOT}/bin:$ENV{PATH})

# OpenSSL build
ExternalProject_Add(
  openssl-project
  URL https://www.openssl.org/source/openssl-${OPENSSL_VERSION}.tar.gz
  URL_MD5 ${OPENSSL_URL}
  SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/openssl
  BINARY_DIR ${CMAKE_CURRENT_BINARY_DIR}/openssl-build
  CONFIGURE_COMMAND
    PATH=${ANDROID_PATH}
    ANDROID_NDK=${ANDROID_NDK}
    ${CMAKE_CURRENT_SOURCE_DIR}/openssl/Configure
      --prefix=${INSTALL_PREFIX}
      -ffunction-sections -fdata-sections
      no-autoerrinit no-autoload-config no-egd no-engine no-hw
      no-ssl no-tests no-tls
      android-${ANDROID_ARCH_NAME}
  BUILD_COMMAND
    PATH=${ANDROID_PATH}
    ANDROID_NDK=${ANDROID_NDK}
    make -j
  INSTALL_COMMAND
    PATH=${ANDROID_PATH}
    ANDROID_NDK=${ANDROID_NDK}
    make install_sw DESTDIR=${INSTALL_DIR}
)

# libcx build
ExternalProject_Add(
  libcx-project
  SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/libcx
  BINARY_DIR ${CMAKE_CURRENT_BINARY_DIR}/libcx-build
  DOWNLOAD_COMMAND
    test -e ${CMAKE_CURRENT_SOURCE_DIR}/libcx/configure ||
      ${CMAKE_CURRENT_SOURCE_DIR}/libcx/autogen.sh
  CONFIGURE_COMMAND
    PATH=${ANDROID_PATH}
    ${CMAKE_CURRENT_SOURCE_DIR}/libcx/configure
      --build=
      --host=${CMAKE_LIBRARY_ARCHITECTURE}
      --prefix=${INSTALL_PREFIX}
      --enable-jni
      "CC=${CMAKE_C_COMPILER}"
      "CPPFLAGS=-I${INSTALL_INCLUDEDIR}"
      "CFLAGS=--target=${CMAKE_C_COMPILER_TARGET} \
              -ffunction-sections -fdata-sections"
      "LDFLAGS=-XCClinker --target=${CMAKE_C_COMPILER_TARGET} \
               -L${INSTALL_LIBDIR}"
  BUILD_COMMAND
    PATH=${ANDROID_PATH}
    make -j
  INSTALL_COMMAND
    PATH=${ANDROID_PATH}
    make install DESTDIR=${INSTALL_DIR}
)
add_dependencies(libcx-project openssl-project)

# Final JNI library
add_library(cxjni SHARED cxjni-wrap.c)
add_dependencies(cxjni openssl-project libcx-project)
target_include_directories(cxjni PRIVATE ${INSTALL_INCLUDEDIR})
target_link_libraries(
  cxjni
  -Wl,-Bstatic -Wl,-Bsymbolic -Wl,--as-needed
  -Wl,--exclude-libs=libcx.a
  -Wl,--exclude-libs=libssl.a
  -Wl,--exclude-libs=libcrypto.a
  -L${INSTALL_LIBDIR}
  -lcxjni -lcx -lssl -lcrypto
  -Wl,-Bdynamic
  -Wl,--gc-sections -Wl,--strip-all
)
